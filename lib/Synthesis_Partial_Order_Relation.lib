
% The mathematical formulation of the next computations can be found in    %
% H. Marchand, M. Samaan,  Incremental Design of a Power Transformer Station %  
% Controller using Controller Synthesis Methodology,   IEEE Transaction on   %  
% Software Engineering,  26(8):729-741, Août 2000%  
  

% the set "duplicate_states" that can be obtained by %
% duplicate_states  declare_suff(state_var(S));  %  


% Given a cost functions C(X) and a system S, the next function gives access   % 
% to a controlled system such that whatever the current position of the system % 
% S under control, the supervisor will make the system evolve into the state x % 
% such that forall x' reachable from the current position C(x)<=C(x')          % 


% Some technical considerations % 
% Input: C1(X) is the cost function used for the control    % 
%        C2(X__1) is the duplicated cost function of C1(X)  %
% where X_11 is, for example obtained as follows:           %  
% Sigali> duplicate_states :   declare_suff(state_var(S));  %
% next one have to declare C2 (i.e. same as for C1 but with %  
% the variables of  the variables set duplicate_states      %  
% Nb. The name of these variables is X__1, where X was a    % 
% already declared variable.                                %  

def Lower_than(S,C1,C2,duplicate_states) : 
   with 
	% R(x,x__1)=0 <=>  C_global(x) <= C_global(x__1)%
	R_Q = a_cost2rel(C1,C2,state_var(S),duplicate_states),
	% R(x__1,x)=0 <=> C_global(x__1) <= C_global(x)% 
	R_Q2 = a_cost2rel(C2,C1,duplicate_states,state_var(S)),
	%R_Strict(x',x)=0 =  R(x,x') and not(R(x',x))% 
	R_Strict =  intersection(R_Q,complementary(R_Q2)),
	% U -> U__1 %   
	controllables2  =  declare_suff(controllable_var(S)),
	% X'=P(X,Y,U__1) % 	
	evolutions2   =   l_rename(evolution(S), 
				   controllable_var(S), controllables2),
	double_evolutions = concat(evolution(S), evolutions2),
	double_states = union_lvar(state_var(S),duplicate_states),
	% R_strict(P(X,Y,U'),P(X,Y,U))=0 % 
	% i.e. we want the next state to be as good as possible %  
	% according to the order relation R_strict%  
	R_Next =  subst(R_Strict, double_states, double_evolutions),

	% The control policy is then to choose among the possible % 
	% values of the controllable variables, the one that   %  
	% will make the system evolve into the state with the best quality%  
	% This is given by adding the  following equation to the original system%  
	% C(X,Y,U)= Q \oplus (1- exists_U'(Q2\oplus R_Next))% 

		% Q(X,Y,U__1)=0 %  
	Q2 = rename(constraint(S), controllable_var(S),controllables2),

	CONTROLLER = 
	intersection(constraint(S), 
		     complementary(exist(intersection(Q2,R_Next),
					 controllables2)))
	do 
	system(event_var(S),state_var(S), evolution(S),
                 [initial(S)], [CONTROLLER],controllable_var(S));





% Given a cost functions C(X) and a system S, the next function gives access   % 
% to a controlled system such that whatever the current position of the system % 
% S under control, the supervisor will make the system evolve into the state x % 
% such that forall x' reachable from the current position C(x)< C(x')          % 

def Strictly_Lower_than(S,C1,C2,duplicate_states) : 
   with
	R_Q = a_cost2rel(C2,C1,duplicate_states,state_var(S)),
	R_Q2 = a_cost2rel(C1,C2,state_var(S),duplicate_states),
	R_Strict =  intersection(R_Q,complementary(R_Q2)),
	controllables2  =  declare_suff(controllable_var(S)),
	evolutions2   =   l_rename(evolution(S), 
				   controllable_var(S), controllables2),
	double_evolutions = concat(evolution(S), evolutions2),
	double_states = union_lvar(state_var(S),duplicate_states),
	R_Next =  subst(R_Strict, double_states, double_evolutions),
	Q2 = rename(constraint(S), controllable_var(S),controllables2),
	CONTROLLER = 
	intersection(constraint(S), 
		     complementary(exist(intersection(Q2,R_Next),
					 controllables2)))
	do 
	system(event_var(S),state_var(S), evolution(S),
                 [initial(S)], [CONTROLLER],controllable_var(S));



% Given a cost functions C(X) and a system S, the next function gives access   % 
% to a controlled system such that whatever the current position of the system % 
% S under control, the supervisor will make the system evolve into the state x % 
% such that forall x' reachable from the current position C(x)>= C(x')         % 

def Greater_than(S,C1,C2,duplicate_states) : 
   with
	R_Q = a_cost2rel_inv(C1,C2,state_var(S),duplicate_states),
	R_Q2 = a_cost2rel_inv(C2,C1,duplicate_states,state_var(S)),
	R_Strict =  intersection(R_Q,complementary(R_Q2)),
	controllables2  =  declare_suff(controllable_var(S)),
	evolutions2   =   l_rename(evolution(S), 
				   controllable_var(S), controllables2),
	double_evolutions = concat(evolution(S), evolutions2),
	double_states = union_lvar(state_var(S),duplicate_states),
	R_Next =  subst(R_Strict, double_states, double_evolutions),
	Q2 = rename(constraint(S), controllable_var(S),controllables2),
	CONTROLLER = 
	intersection(constraint(S), 
		     complementary(exist(intersection(Q2,R_Next),
					 controllables2)))
	do 
	system(event_var(S),state_var(S), evolution(S),
                 [initial(S)], [CONTROLLER],controllable_var(S));

% Given a cost functions C(X) and a system S, the next function gives access   % 
% to a controlled system such that whatever the current position of the system % 
% S under control, the supervisor will make the system evolve into the state x % 
% such that forall x' reachable from the current position C(x)> C(x')          % 

def Strictly_Greater_than(S,C1,C2,duplicate_states) : 
   with
	R_Q = a_cost2rel_inv(C2,C1,duplicate_states,state_var(S)),
	R_Q2 = a_cost2rel_inv(C1,C2,state_var(S),duplicate_states),
	R_Strict =  intersection(R_Q,complementary(R_Q2)),
	controllables2  =  declare_suff(controllable_var(S)),
	evolutions2   =   l_rename(evolution(S), 
				   controllable_var(S), controllables2),
	double_evolutions = concat(evolution(S), evolutions2),
	double_states = union_lvar(state_var(S),duplicate_states),
	R_Next =  subst(R_Strict, double_states, double_evolutions),
	Q2 = rename(constraint(S), controllable_var(S),controllables2),
	CONTROLLER = 
	intersection(constraint(S), 
		     complementary(exist(intersection(Q2,R_Next),
					 controllables2)))
	do 
	system(event_var(S),state_var(S), evolution(S),
                 [initial(S)], [CONTROLLER],controllable_var(S));



